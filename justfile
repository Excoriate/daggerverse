export NIXPKGS_ALLOW_UNFREE := "1"
export NOTHANKS := "1"

default:
  @just --list

# --------------------------------------------------
# Section: Nix Development Environment
# --------------------------------------------------
# This section contains recipes for setting up and managing
# the Nix development environment, including entering the environment,
# cleaning caches, and initializing the project.
# --------------------------------------------------

# Recipe to enter the Nix development environment üß∞
dev:
  @echo "Entering Nix development environment üß∞ ..."
  @nix develop --impure --extra-experimental-features nix-command --extra-experimental-features flakes

# Recipe to clean Go cache, Go modules cache, and Nix/DevEnv/DirEnv cache üßπ
clean-nix-cache:
  @echo "Cleaning Go cache üßπ ..."
  @go clean -cache
  @echo "Cleaning Go modules cache üßπ ..."
  @go clean -modcache
  @echo "Cleaning Nix/DevEnv/DirEnv cache üßπ ..."
  @nix-collect-garbage -d

# Recipe to run pre-commit hooks üîç
run-hooks:
  @echo "Running pre-commit hooks üîç ..."
  @nix-shell -p pre-commit --run "pre-commit run --all-files"
  @echo "Pre-commit hooks passed ‚úÖ"

# Recipe to bump version of a module üîÑ
bump-version mod bump='minor':
    #!/usr/bin/env bash
    set -euo pipefail

    echo "üîÑ Bumping version for {{mod}} module"

    # Verify that the module directory exists and contains a dagger.json file
    if [ ! -d "{{mod}}" ] || [ ! -f "{{mod}}/dagger.json" ]; then
        echo "‚ùå Module {{mod}} not found or dagger.json missing"
        exit 1
    fi

    # Get the latest tag for this module
    latest_tag=$(git describe --tags --abbrev=0 --match "{{mod}}/*" 2>/dev/null || echo "{{mod}}/v0.0.0")
    current_version=$(echo $latest_tag | sed 's/{{mod}}\/v//')

    # Calculate the new version
    new_version="v$(semver bump {{bump}} "v$current_version")"

    echo "üî¢ Current version: v$current_version"
    echo "üÜï New version: $new_version"

    read -p "Proceed with version bump? (y/N) " -n 1 -r
    echo
    if [[ ! $REPLY =~ ^[Yy]$ ]]; then
        echo "‚ùå Aborting"
        exit 1
    fi

    # Create and push the new tag
    new_tag="{{mod}}/$new_version"
    git tag -a "$new_tag" -m "Bump {{mod}} to $new_version"
    git push origin "$new_tag"

    echo "‚úÖ Version bumped to $new_version and tag $new_tag created"
    echo "üöÄ Tag has been pushed to the remote repository"

# --------------------------------------------------
# Section: Recipes for running tests, examples, and linting
# ------------------------------------------------------------------------------
# This section contains recipes for running tests, examples, and linting in a certain module.
# cleaning caches, and initializing the project.
# --------------------------------------------------

# Recipe to run all the tests in the target module üß™
test mod *args: (reloadmod mod) (reloadtest mod)
  @echo "üöÄ Running Dagger module tests in module [{{mod}}]..."
  @echo "üì¶ Currently in {{mod}} module üß™, path=`pwd`/{{mod}}/tests"
  @cd {{mod}}/tests && dagger functions
  @cd {{mod}}/tests && dagger call test-all {{args}}

# Recipe to run all the examples in the target module üìÑ
examplesgo mod *args: (reloadmod mod) (reloadexamples mod)
  @echo "üöÄ Running Dagger module examples (Go SDK)..."
  @echo "üì¶ Currently in {{mod}} module examples/go üìÑ, path=`pwd`/{{mod}}/examples/go"
  @cd {{mod}}/examples/go && dagger call all-recipes {{args}}

# Recipe to run GolangCI Lint in top-level moduleüßπ
lintmod mod *args: (reloadmod mod)
  @echo "Running Go (GolangCI)... üßπ in module [{{mod}}] üì¶"
  @echo "üì¶ Currently in {{mod}} module, path=`pwd`/{{mod}}"
  @cd ./{{mod}} && nix-shell -p golangci-lint --run "golangci-lint run --config ../.golangci.yml {{args}}"
  @echo "üìÑ Checking now the examples project ..."
  @cd ./{{mod}}/examples/go && nix-shell -p golangci-lint --run "golangci-lint run --config ../../../.golangci.yml {{args}}"

# Recipe to run GolangCI Lint in tests module üßπ
linttests mod *args: (reloadtest mod)
  @echo "Running Go (GolangCI)... üßπ in module [{{mod}}/tests] üß™"
  @echo "üì¶ Currently in {{mod}}/tests module, path=`pwd`/{{mod}}/tests"
  @cd ./{{mod}}/tests && nix-shell -p golangci-lint --run "golangci-lint run --config ../../.golangci.yml {{args}}"

# Recipe to run GolangCI Lint in examples/go module üßπ
lintexamples mod *args: (reloadexamples mod)
  @echo "Running Go (GolangCI)... üßπ in module [{{mod}}/examples/go] üìÑ"
  @echo "üì¶ Currently in {{mod}}/examples/go module, path=`pwd`"
  @cd ./{{mod}}/examples/go && nix-shell -p golangci-lint --run "golangci-lint run --config ../../../.golangci.yml {{args}}"

# Recipe to run GolangCI Lint in all modules üßπ
lintall mod: (lintmod mod) (linttests mod) (lintexamples mod)
  @echo "‚úÖ All Go (GolangCI) lint checks passed ‚úÖ"

# Recipe to run the whole CI locally üöÄ
ci mod: (reloadall mod) (lintall mod) (test mod) (examplesgo mod) (ci-mod-docs mod)
  @echo "üéâ All checks passed! üéâ"

# Recipe to validate if the dagger module has the README.md file and the LICENSE file üìÑ
ci-mod-docs mod:
  @echo "üîç Validating the module documentation..."
  @test -f {{mod}}/README.md || (echo "‚ùå README.md file not found" && exit 1)
  @test -f {{mod}}/LICENSE || (echo "‚ùå LICENSE file not found" && exit 1)
  @echo "‚úÖ Module documentation is valid"

# --------------------------------------------------
# Section: Dagger Functions
# --------------------------------------------------
# This section contains recipes for calling functions
# in a certain module.
# --------------------------------------------------

# Recipe to call an specific function from the examples/go project in a certain module üìû
callfnexample mod *args: (check-dagger-pre-requisites mod) (reloadexamples mod)
  @echo "üîß Calling a function in the üìÑ examples/go module [{{mod}}/examples/go]..."
  @echo "üì¶ Currently in [{{mod}}/examples/go] module, path=`pwd`"
  @cd {{mod}}/examples/go && dagger call {{args}}

# Recipe for dagger call tests in a certain module üß™
callfntest mod *args: (check-dagger-pre-requisites mod) (reloadtest mod)
  @echo "üî® Calling a function {{args}} in the üß™ test module [{{mod}}/tests]..."
  @echo "üì¶ Currently in [{{mod}}/tests] module, path=`pwd`/{{mod}}/tests"
  @cd {{mod}}/tests && dagger functions
  @cd {{mod}}/tests && dagger call {{args}}

# Recipe to call a certain function by a module's name, passing extra arguments optionally üìû
callfn mod *args: (check-dagger-pre-requisites mod) (reloadmod mod)
  @echo "üî® Calling a function {{args}} in the module [{{mod}}]..."
  @echo "üìÇ Currently in [{{mod}}] module, path=`pwd`/{{mod}}"
  @cd {{mod}} && dagger functions
  @cd {{mod}} && dagger call {{args}}

# Recipe to list functions in a certain module üìÑ
listfns mod *args: (check-dagger-pre-requisites mod) (reloadmod mod)
  @echo "üìÑ Retrieving available functions for the module..."
  @echo "üì¶ Currently in [{{mod}}] module, path=`pwd`/{{mod}}"
  @cd {{mod}} && dagger functions

# Recipe to list functions in a test üß™ module
listfnstest mod *args: (check-dagger-pre-requisites mod) (reloadtest mod)
  @echo "üìÑ Retrieving available functions for the module..."
  @echo "üì¶ Currently in [{{mod}}/tests] module, path=`pwd`/{{mod}}/tests"
  @cd {{mod}}/tests && dagger functions {{args}}

# Recipe to list functions in a examples/go üìÑ module
listfnsexamples mod *args: (check-dagger-pre-requisites mod) (reloadexamples mod)
  @echo "üìÑ Retrieving available functions for the module..."
  @echo "üì¶ Currently in [{{mod}}/examples/go] module, path=`pwd`"
  @cd {{mod}}/examples/go && dagger functions {{args}}

# --------------------------------------------------
# Section: Dagger Maintenance
# --------------------------------------------------
# This section contains recipes for various maintenance
# operations related to Dagger modules.
# --------------------------------------------------

# Recipe that wraps the dagger CLI in a certain module üì¶
dagcli mod *args: (check-dagger-pre-requisites mod)
  @echo "üöÄ Running Dagger CLI in a certain module..."
  @echo "üì¶ Currently in [{{mod}}] module, path=`pwd`/{{mod}}"
  @cd {{mod}} && dagger {{args}}

# Recipe to run dagger develop and if the engine gots updated, update the modules üîÑ
update-all: (daggy-compile) (check-docker-or-podman)
  @echo "üöÄ Developing (or upgrading) all Dagger modules..."
  @.daggerx/daggy/target/release/daggy --task=develop

# --------------------------------------------------
# Section: Creating new modules
# --------------------------------------------------
# This section contains recipes for various operations
# related to Dagger modules, such as calling functions,
# listing functions, and running tests.
# --------------------------------------------------

# Recipe to create a new module using Daggy (a rust CLI tool) üõ†Ô∏è
create mod with-ci='false' type='full': (daggy-compile) (check-docker-or-podman)
  @echo "üöÄ Creating a new {{type}} module of type {{type}}..."
  @.daggerx/daggy/target/release/daggy --task=create --module={{mod}} --module-type={{type}}
  @if [ "{{with-ci}}" = "true" ]; then just cilocal {{mod}}; fi

# Recipe to create a new light module using Daggy üõ†Ô∏è
createlight mod with-ci='false' type='light': (daggy-compile) (check-docker-or-podman)
  @echo "üöÄ Creating a new {{type}} module of type {{type}}..."
  @./.daggerx/daggy/target/release/daggy --task=create --module={{mod}} --module-type={{type}}
  @if [ "{{with-ci}}" = "true" ]; then just cilocal {{mod}}; fi

# Recipe to inspect module-template (full, or light) changes that require sync in the Go templates. üîç
inspect type='full' detailed='false': (daggy-compile) (check-docker-or-podman)
  @echo "üîç Inspecting the module..."
  @./.daggerx/daggy/target/release/daggy --task=inspect --module-type={{type}} --detailed={{detailed}}

sync type='full': (daggy-compile) (check-docker-or-podman)
  @echo "üîç Syncing the module..."
  @./.daggerx/daggy/target/release/daggy --task=sync --module-type={{type}} --detailed={{detailed}}

# --------------------------------------------------
# Section: Daggy Operations
# --------------------------------------------------
# This section contains recipes for compiling and testing
# the Daggy tool.
# --------------------------------------------------

# Recipe to run Daggy tests üß™
daggy-tests: (daggy-compile)
  @echo "Running Daggy tests üß™ ..."
  @cd .daggerx/daggy && cargo test

# Recipe to compile Daggy üîÑ
daggy-compile:
  @echo "Compiling Daggy üîÑ ..."
  @cd .daggerx/daggy && cargo build --release
  @echo "Daggy compiled successfully üîÑ"

# --------------------------------------------------
# Section: Reloading Dagger Modules
# --------------------------------------------------
# This section contains recipes for reloading Dagger
# modules and their tests.
# --------------------------------------------------

# Recipe to reload Dagger module (Dagger Develop) üîÑ
reloadmod mod *args: (check-dagger-pre-requisites mod)
  @echo "üöÄ Running Dagger development in a given module..."
  @echo "üì¶ Currently in [{{mod}}] module, path=`pwd`/{{mod}}"
  @cd {{mod}} && dagger develop {{args}}
  @echo "‚úÖ Module reloaded successfully"

# Recipe to reload a Dagger module's tests (Dagger Develop) üîÑ
reloadtest mod *args: (check-dagger-pre-requisites mod)
  @echo "üöÄ Running Dagger development in a given module's tests..."
  @echo "üì¶ Currently in [{{mod}}/tests] module, path=`pwd`/{{mod}}/tests"
  @cd {{mod}}/tests && dagger develop {{args}}
  @echo "‚úÖ Module Tests reloaded successfully"

# Recipe to reload the Dagger module's examples (examples/go) üîÑ
reloadexamples mod *args: (check-dagger-pre-requisites mod)
  @echo "üöÄ Reloading the module's examples..."
  @echo "üì¶ Currently in {{mod}}/examples/go module, path=`pwd`"
  @test -d {{mod}}/examples/go || (echo "‚ùå Module examples not found" && exit 1)
  @cd {{mod}}/examples/go && dagger develop {{args}}
  @echo "üöÄ Module's examples reloaded successfully"

# Recipe to reload Dagger module and its underlying tests (Dagger Develop & Dagger Call/Functions) üîÑ
reloadall mod *args: (reloadmod mod) (reloadtest mod) (reloadexamples mod)
  @echo "üîÑ Reloading all the module, tests and examples... [{{mod}}]"
  @echo "üöÄ Module reloaded successfully"


# --------------------------------------------------
# Section: Utilities
# --------------------------------------------------
# This section contains recipes for utilities that
# are used to validate if Docker or Podman is running
# and to validate if a given directory is a Dagger module.
# --------------------------------------------------

# Recipe to check if Docker or Podman is running üîÑ
check-docker-or-podman:
  #!/usr/bin/env sh
  set -e

  if command -v docker > /dev/null 2>&1; then
    if ! docker info > /dev/null 2>&1; then
      echo "‚ùå Docker is installed but not running. Please start Docker and try again."
      exit 1
    else
      echo "‚úÖ Docker is running."
    fi
  elif command -v podman > /dev/null 2>&1; then
    if ! podman info > /dev/null 2>&1; then
      echo "‚ùå Podman is installed but not running. Please start Podman and try again."
      exit 1
    else
      echo "‚úÖ Podman is running."
    fi
  else
    echo "‚ùå Neither Docker nor Podman is installed. Please install one of them and try again."
    exit 1
  fi

# Recipe that validate if it's an actual Dagger module
is-dagger-module mod:
  @echo "üîç Validating if [{{mod}}] is a Dagger module..."
  @test -d {{mod}} || (echo "‚ùå Module not found at path=`pwd`/{{mod}}" && exit 1)
  @test -f {{mod}}/dagger.json || (echo "‚ùå dagger.json not found in module at path=`pwd`/{{mod}}. Not a Dagger module." && exit 1)
  @echo "‚úÖ [{{mod}}] is a Dagger module"


# Recipe to format Go files in Dagger modules, excluding internal/ and dagger.gen.go
fmt mod:
    #!/usr/bin/env sh
    set -e

    echo "üîç Checking and formatting Go files in [{{mod}}] and its submodules..."
    
    formatted_files=()

    # Function to format files in a directory if it's a Dagger module
    format_directory() {
        if [ -f "$1/dagger.json" ]; then
            echo "‚úÖ Formatting Dagger module: $1"
            while IFS= read -r file; do
                gofmt -s -w "$file"
                formatted_files+=("$file")
            done < <(find "$1" -name '*.go' ! -path '*/internal/*' ! -name 'dagger.gen.go')
        else
            echo "‚ÑπÔ∏è Skipping non-Dagger module: $1"
        fi
    }

    # Format parent module
    format_directory "{{mod}}"

    # Format tests submodule
    if [ -d "{{mod}}/tests" ]; then
        format_directory "{{mod}}/tests"
    fi

    # Format examples/go submodule
    if [ -d "{{mod}}/examples/go" ]; then
        format_directory "{{mod}}/examples/go"
    fi

    if [ ${#formatted_files[@]} -eq 0 ]; then
        echo "‚úÖ No files required formatting in Dagger modules within [{{mod}}] and its submodules."
    else
        echo "‚úÖ Formatted the following files in Dagger modules within [{{mod}}] and its submodules:"
        printf '%s\n' "${formatted_files[@]}"
    fi

# Recipe that check Dagger pre-requisites
check-dagger-pre-requisites mod: (check-docker-or-podman) (is-dagger-module mod)