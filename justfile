export NIXPKGS_ALLOW_UNFREE := "1"
export NOTHANKS := "1"

default:
  @just --list

# --------------------------------------------------
# Section: Nix Development Environment
# --------------------------------------------------
# This section contains recipes for setting up and managing
# the Nix development environment, including entering the environment,
# cleaning caches, and initializing the project.
# --------------------------------------------------

# Recipe to enter the Nix development environment ğŸ§°
dev:
  @echo "Entering Nix development environment ğŸ§° ..."
  @nix develop

# Recipe to clean Go cache, Go modules cache, and Nix/DevEnv/DirEnv cache ğŸ§¹
clean-all:
  @echo "Cleaning Go cache ğŸ§¹ ..."
  @go clean -cache
  @echo "Cleaning Go modules cache ğŸ§¹ ..."
  @go clean -modcache
  @echo "Cleaning Nix/DevEnv/DirEnv cache ğŸ§¹ ..."
  @nix-collect-garbage -d

fmt:
  @echo "Formatting code ğŸ” ..."
  @nix fmt

# Recipe to run pre-commit hooks ğŸ”
run-hooks:
  @echo "Running pre-commit hooks ğŸ” ..."
  @nix develop .# --command bash -c "pre-commit run --all-files"
  @echo "Pre-commit hooks passed âœ…"

# Recipe to bump version of a module ğŸ”„
bump-version mod bump='minor':
    #!/usr/bin/env bash
    set -euo pipefail

    echo "ğŸ”„ Bumping version for {{mod}} module"

    # Verify that the module directory exists and contains a dagger.json file
    if [ ! -d "{{mod}}" ] || [ ! -f "{{mod}}/dagger.json" ]; then
        echo "âŒ Module {{mod}} not found or dagger.json missing"
        exit 1
    fi

    # Get the latest tag for this module
    latest_tag=$(git describe --tags --abbrev=0 --match "{{mod}}/*" 2>/dev/null || echo "{{mod}}/v0.0.0")
    current_version=$(echo $latest_tag | sed 's/{{mod}}\/v//')

    # Calculate the new version
    new_version="v$(semver bump {{bump}} "v$current_version")"

    echo "ğŸ”¢ Current version: v$current_version"
    echo "ğŸ†• New version: $new_version"

    read -p "Proceed with version bump? (y/N) " -n 1 -r
    echo
    if [[ ! $REPLY =~ ^[Yy]$ ]]; then
        echo "âŒ Aborting"
        exit 1
    fi

    # Create and push the new tag
    new_tag="{{mod}}/$new_version"
    git tag -a "$new_tag" -m "Bump {{mod}} to $new_version"
    git push origin "$new_tag"

    echo "âœ… Version bumped to $new_version and tag $new_tag created"
    echo "ğŸš€ Tag has been pushed to the remote repository"

# --------------------------------------------------
# Section: Recipes for running tests, examples, and linting
# ------------------------------------------------------------------------------
# This section contains recipes for running tests, examples, and linting in a certain module.
# cleaning caches, and initializing the project.
# --------------------------------------------------

# Recipe to run all the tests in the target module ğŸ§ª
test mod *args: (reloadmod mod) (reloadtest mod)
  @echo "ğŸš€ Running Dagger module tests in module [{{mod}}]..."
  @echo "ğŸ“¦ Currently in {{mod}} module ğŸ§ª, path=`pwd`/{{mod}}/tests"
  @cd {{mod}}/tests && dagger functions
  @cd {{mod}}/tests && dagger call test-all {{args}}

# Recipe to run all the examples in the target module ğŸ“„
examplesgo mod *args: (reloadmod mod) (reloadexamples mod)
  @echo "ğŸš€ Running Dagger module examples (Go SDK)..."
  @echo "ğŸ“¦ Currently in {{mod}} module examples/go ğŸ“„, path=`pwd`/{{mod}}/examples/go"
  @cd {{mod}}/examples/go && dagger call all-recipes {{args}}

# Recipe to run GolangCI Lint in top-level moduleğŸ§¹
lintmod mod *args: (reloadmod mod)
  @echo "Running Go (GolangCI)... ğŸ§¹ in module [{{mod}}] ğŸ“¦"
  @nix develop .# --command bash -c "cd {{mod}} && golangci-lint run --config ../.golangci.yml --verbose {{args}}"

# Recipe to run GolangCI Lint in tests module ğŸ§¹
linttests mod *args: (reloadtest mod)
  @echo "Running Go (GolangCI)... ğŸ§¹ in module [{{mod}}/tests] ğŸ§ª"
  @nix develop .# --command bash -c "cd {{mod}}/tests && golangci-lint run --config ../../.golangci.yml --verbose {{args}}"

# Recipe to run GolangCI Lint in examples/go module ğŸ§¹
lintexamples mod *args: (reloadexamples mod)
  @echo "Running Go (GolangCI)... ğŸ§¹ in module [{{mod}}/examples/go] ğŸ“„"
  @nix develop .# --command bash -c "cd {{mod}}/examples/go && golangci-lint run --config ../../../.golangci.yml --verbose {{args}}"

# Recipe to run GolangCI Lint in all modules ğŸ§¹
lintall mod: (lintmod mod) (linttests mod) (lintexamples mod)
  @echo "âœ… All Go (GolangCI) lint checks passed âœ…"

# Recipe to run the whole CI locally ğŸš€
ci mod: (reloadall mod) (lintall mod) (test mod) (examplesgo mod) (ci-mod-docs mod)
  @echo "ğŸ‰ All checks passed! ğŸ‰"

# Recipe to validate if the dagger module has the README.md file and the LICENSE file ğŸ“„
ci-mod-docs mod:
  @echo "ğŸ” Validating the module documentation..."
  @test -f {{mod}}/README.md || (echo "âŒ README.md file not found" && exit 1)
  @test -f {{mod}}/LICENSE || (echo "âŒ LICENSE file not found" && exit 1)
  @echo "âœ… Module documentation is valid"

# --------------------------------------------------
# Section: Dagger Functions
# --------------------------------------------------
# This section contains recipes for calling functions
# in a certain module.
# --------------------------------------------------

# Recipe to call an specific function from the examples/go project in a certain module ğŸ“
callfnexample mod *args: (check-dagger-pre-requisites mod) (reloadexamples mod)
  @echo "ğŸ”§ Calling a function in the ğŸ“„ examples/go module [{{mod}}/examples/go]..."
  @echo "ğŸ“¦ Currently in [{{mod}}/examples/go] module, path=`pwd`"
  @cd {{mod}}/examples/go && dagger call {{args}}

# Recipe for dagger call tests in a certain module ğŸ§ª
callfntest mod *args: (check-dagger-pre-requisites mod) (reloadtest mod)
  @echo "ğŸ”¨ Calling a function {{args}} in the ğŸ§ª test module [{{mod}}/tests]..."
  @echo "ğŸ“¦ Currently in [{{mod}}/tests] module, path=`pwd`/{{mod}}/tests"
  @cd {{mod}}/tests && dagger functions
  @cd {{mod}}/tests && dagger call {{args}}

# Recipe to call a certain function by a module's name, passing extra arguments optionally ğŸ“
callfn mod *args: (check-dagger-pre-requisites mod) (reloadmod mod)
  @echo "ğŸ”¨ Calling a function {{args}} in the module [{{mod}}]..."
  @echo "ğŸ“‚ Currently in [{{mod}}] module, path=`pwd`/{{mod}}"
  @cd {{mod}} && dagger functions
  @cd {{mod}} && dagger call {{args}}

# Recipe to list functions in a certain module ğŸ“„
listfns mod *args: (check-dagger-pre-requisites mod) (reloadmod mod)
  @echo "ğŸ“„ Retrieving available functions for the module..."
  @echo "ğŸ“¦ Currently in [{{mod}}] module, path=`pwd`/{{mod}}"
  @cd {{mod}} && dagger functions

# Recipe to list functions in a test ğŸ§ª module
listfnstest mod *args: (check-dagger-pre-requisites mod) (reloadtest mod)
  @echo "ğŸ“„ Retrieving available functions for the module..."
  @echo "ğŸ“¦ Currently in [{{mod}}/tests] module, path=`pwd`/{{mod}}/tests"
  @cd {{mod}}/tests && dagger functions {{args}}

# Recipe to list functions in a examples/go ğŸ“„ module
listfnsexamples mod *args: (check-dagger-pre-requisites mod) (reloadexamples mod)
  @echo "ğŸ“„ Retrieving available functions for the module..."
  @echo "ğŸ“¦ Currently in [{{mod}}/examples/go] module, path=`pwd`"
  @cd {{mod}}/examples/go && dagger functions {{args}}

# --------------------------------------------------
# Section: Dagger Maintenance
# --------------------------------------------------
# This section contains recipes for various maintenance
# operations related to Dagger modules.
# --------------------------------------------------

# Recipe that wraps the dagger CLI in a certain module ğŸ“¦
dagcli mod *args: (check-dagger-pre-requisites mod)
  @echo "ğŸš€ Running Dagger CLI in a certain module..."
  @echo "ğŸ“¦ Currently in [{{mod}}] module, path=`pwd`/{{mod}}"
  @cd {{mod}} && dagger {{args}}

# Recipe to run dagger develop and if the engine gots updated, update the modules ğŸ”„
update-all: (daggy-compile) (check-docker-or-podman)
  @echo "ğŸš€ Developing (or upgrading) all Dagger modules..."
  @.daggerx/daggy/target/release/daggy --task=develop

# --------------------------------------------------
# Section: Creating new modules
# --------------------------------------------------
# This section contains recipes for various operations
# related to Dagger modules, such as calling functions,
# listing functions, and running tests.
# --------------------------------------------------

# Recipe to create a new module using Daggy (a rust CLI tool) ğŸ› ï¸
create mod with-ci='false' type='full': (daggy-compile) (check-docker-or-podman)
  @echo "ğŸš€ Creating a new {{type}} module of type {{type}}..."
  @.daggerx/daggy/target/release/daggy --task=create --module={{mod}} --module-type={{type}}
  @if [ "{{with-ci}}" = "true" ]; then just cilocal {{mod}}; fi

# Recipe to create a new light module using Daggy ğŸ› ï¸
createlight mod with-ci='false' type='light': (daggy-compile) (check-docker-or-podman)
  @echo "ğŸš€ Creating a new {{type}} module of type {{type}}..."
  @./.daggerx/daggy/target/release/daggy --task=create --module={{mod}} --module-type={{type}}
  @if [ "{{with-ci}}" = "true" ]; then just cilocal {{mod}}; fi

# --------------------------------------------------
# Section: Daggy Operations
# --------------------------------------------------
# This section contains recipes for compiling and testing
# the Daggy tool.
# --------------------------------------------------

# Recipe to run Daggy tests ğŸ§ª
daggy-tests: (daggy-compile)
  @echo "Running Daggy tests ğŸ§ª ..."
  @cd .daggerx/daggy && cargo test

# Recipe to compile Daggy ğŸ”„
daggy-compile:
  @echo "Compiling Daggy ğŸ”„ ..."
  @cd .daggerx/daggy && cargo build --release
  @echo "Daggy compiled successfully ğŸ”„"

# --------------------------------------------------
# Section: Reloading Dagger Modules
# --------------------------------------------------
# This section contains recipes for reloading Dagger
# modules and their tests.
# --------------------------------------------------

# Recipe to reload Dagger module (Dagger Develop) ğŸ”„
reloadmod mod *args: (check-dagger-pre-requisites mod)
  @echo "ğŸš€ Running Dagger development in a given module..."
  @echo "ğŸ“¦ Currently in [{{mod}}] module, path=`pwd`/{{mod}}"
  @cd {{mod}} && dagger develop {{args}}
  @echo "âœ… Module reloaded successfully"

# Recipe to reload a Dagger module's tests (Dagger Develop) ğŸ”„
reloadtest mod *args: (check-dagger-pre-requisites mod)
  @echo "ğŸš€ Running Dagger development in a given module's tests..."
  @echo "ğŸ“¦ Currently in [{{mod}}/tests] module, path=`pwd`/{{mod}}/tests"
  @cd {{mod}}/tests && dagger develop {{args}}
  @echo "âœ… Module Tests reloaded successfully"

# Recipe to reload the Dagger module's examples (examples/go) ğŸ”„
reloadexamples mod *args: (check-dagger-pre-requisites mod)
  @echo "ğŸš€ Reloading the module's examples..."
  @echo "ğŸ“¦ Currently in {{mod}}/examples/go module, path=`pwd`"
  @test -d {{mod}}/examples/go || (echo "âŒ Module examples not found" && exit 1)
  @cd {{mod}}/examples/go && dagger develop {{args}}
  @echo "ğŸš€ Module's examples reloaded successfully"

# Recipe to reload Dagger module and its underlying tests (Dagger Develop & Dagger Call/Functions) ğŸ”„
reloadall mod *args: (reloadmod mod) (reloadtest mod) (reloadexamples mod)
  @echo "ğŸ”„ Reloading all the module, tests and examples... [{{mod}}]"
  @echo "ğŸš€ Module reloaded successfully"


# --------------------------------------------------
# Section: Utilities
# --------------------------------------------------
# This section contains recipes for utilities that
# are used to validate if Docker or Podman is running
# and to validate if a given directory is a Dagger module.
# --------------------------------------------------

# Recipe to check if Docker or Podman is running ğŸ”„
check-docker-or-podman:
  #!/usr/bin/env sh
  set -e

  if command -v docker > /dev/null 2>&1; then
    if ! docker info > /dev/null 2>&1; then
      echo "âŒ Docker is installed but not running. Please start Docker and try again."
      exit 1
    else
      echo "âœ… Docker is running."
    fi
  elif command -v podman > /dev/null 2>&1; then
    if ! podman info > /dev/null 2>&1; then
      echo "âŒ Podman is installed but not running. Please start Podman and try again."
      exit 1
    else
      echo "âœ… Podman is running."
    fi
  else
    echo "âŒ Neither Docker nor Podman is installed. Please install one of them and try again."
    exit 1
  fi

# Recipe that validate if it's an actual Dagger module
is-dagger-module mod:
  @echo "ğŸ” Validating if [{{mod}}] is a Dagger module..."
  @test -d {{mod}} || (echo "âŒ Module not found at path=`pwd`/{{mod}}" && exit 1)
  @test -f {{mod}}/dagger.json || (echo "âŒ dagger.json not found in module at path=`pwd`/{{mod}}. Not a Dagger module." && exit 1)
  @echo "âœ… [{{mod}}] is a Dagger module"


# Recipe that check Dagger pre-requisites
check-dagger-pre-requisites mod: (check-docker-or-podman) (is-dagger-module mod)

# Recipe to sync module-template (light) changes that require sync in the Go templates. ğŸ”
syncmodlight: (daggy-compile)
  @echo "ğŸ” Syncing the module..."
  @./.daggerx/daggy/target/release/daggy --task=sync --inspect-type=light --detailed=false

# Recipe to sync module-template (full) changes that require sync in the Go templates. ğŸ”
syncmodfull: (daggy-compile)
  @echo "ğŸ” Syncing the module..."
  @./.daggerx/daggy/target/release/daggy --task=sync --inspect-type=full --detailed=false
