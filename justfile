export NIXPKGS_ALLOW_UNFREE := "1"
export NOTHANKS := "1"

default:
  @just --list

# Recipe to run your development environment commands ğŸ§°
dev:
  @echo "Entering Nix development environment ğŸ§° ..."
  @nix develop --impure --extra-experimental-features nix-command --extra-experimental-features flakes

# Recipe to clean Go cache, Go modules cache, and Nix/DevEnv/DirEnv cache ğŸ§¹
clean-cache:
  @echo "Cleaning Go cache ğŸ§¹ ..."
  @go clean -cache
  @echo "Cleaning Go modules cache ğŸ§¹ ..."
  @go clean -modcache
  @echo "Cleaning Nix/DevEnv/DirEnv cache ğŸ§¹ ..."
  @nix-collect-garbage -d

# Recipe to initialize the project ğŸš€
init:
  @echo "Initializing the project ğŸš€ ..."
  @nix-shell -p pre-commit --run "pre-commit install --hook-type pre-commit"
  @echo "Pre-commit hook installed âœ…"
  @nix-shell -p pre-commit --run "pre-commit install --hook-type pre-push"
  @echo "Pre-push hook installed âœ…"
  @nix-shell -p pre-commit --run "pre-commit install --hook-type commit-msg"
  @echo "Commit-msg hook installed âœ…"
  @nix-shell -p pre-commit --run "pre-commit autoupdate"
  @echo "Pre-commit hooks updated to the latest version ğŸ”„"

# Recipe to run pre-commit hooks ğŸ”
precommit:
  @echo "Running pre-commit hooks ğŸ” ..."
  @nix-shell -p pre-commit --run "pre-commit run --all-files"
  @echo "Pre-commit hooks passed âœ…"

# Recipe to run Dagger module ğŸ“¦
dc mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running Dagger module..."
  echo "ğŸ“¦ Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  cd {{mod}} && dagger call {{args}}

# Recipe to run Dagger module tests ğŸ§ª
dct mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸ§ª Running Dagger module tests..."
  echo "ğŸ§ª Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}}/tests || (echo "âŒ Module not found" && exit 1)
  cd {{mod}}/tests && dagger call {{args}}

# Recipe to run Dagger module examples ğŸ“„
dce mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸ“„ Running Dagger module examples ..."
  echo "ğŸ§ª Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}}/examples/go || (echo "âŒ Module examples not found" && exit 1)
  cd {{mod}}/examples/go && dagger call {{args}}

# Recipe to bump version of a module ğŸ”„
bump-version mod bump='minor':
    #!/usr/bin/env bash
    set -euo pipefail

    echo "ğŸ”„ Bumping version for {{mod}} module"

    # Verify that the module directory exists and contains a dagger.json file
    if [ ! -d "{{mod}}" ] || [ ! -f "{{mod}}/dagger.json" ]; then
        echo "âŒ Module {{mod}} not found or dagger.json missing"
        exit 1
    fi

    # Get the latest tag for this module
    latest_tag=$(git describe --tags --abbrev=0 --match "{{mod}}/*" 2>/dev/null || echo "{{mod}}/v0.0.0")
    current_version=$(echo $latest_tag | sed 's/{{mod}}\/v//')

    # Calculate the new version
    new_version="v$(semver bump {{bump}} "v$current_version")"

    echo "ğŸ”¢ Current version: v$current_version"
    echo "ğŸ†• New version: $new_version"

    read -p "Proceed with version bump? (y/N) " -n 1 -r
    echo
    if [[ ! $REPLY =~ ^[Yy]$ ]]; then
        echo "âŒ Aborting"
        exit 1
    fi

    # Create and push the new tag
    new_tag="{{mod}}/$new_version"
    git tag -a "$new_tag" -m "Bump {{mod}} to $new_version"
    git push origin "$new_tag"

    echo "âœ… Version bumped to $new_version and tag $new_tag created"
    echo "ğŸš€ Tag has been pushed to the remote repository"

# Recipe to reload Dagger module (Dagger Develop) ğŸ”„
reloadmod mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running Dagger development in a given module..."
  echo "ğŸ“¦ Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  if ! docker info > /dev/null 2>&1; then
    echo "âŒ Docker is not running. Please start Docker and try again."
    exit 1
  fi
  cd {{mod}} && dagger develop {{args}}
  echo "âœ… Module reloaded successfully"

# Recipe to reload a Dagger module's tests (Dagger Develop) ğŸ”„
reloadtest mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running Dagger development in a given module's tests..."
  echo "ğŸ“¦ Currently in {{mod}}/tests module, path=`pwd`"
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  cd {{mod}}/tests && dagger develop {{args}}
  echo "âœ… Module Tests reloaded successfully"

# Recipe to reload Dagger module and its underlying tests (Dagger Develop & Dagger Call/Functions) ğŸ”„
reloadall mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸ”„ Reloading Dagger module and also the tests..."
  echo "ğŸ“¦ Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  cd {{mod}} && dagger develop {{args}}
  cd tests && dagger develop {{args}}
  cd ../examples/go && dagger develop {{args}}
  echo "ğŸš€ Module reloaded successfully"
  echo "ğŸ•µï¸ Inspecting the module..."
  cd .. && dagger call && dagger functions

# Recipe to run all the tests in the target module ğŸ§ª
test mod *args: (reloadmod mod) (reloadtest mod)
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running Dagger module tests..."
  echo "ğŸ“¦ Currently in {{mod}} module ğŸ§ª, path=`pwd`"
  test -d {{mod}}/tests || (echo "âŒ Module not found" && exit 1)
  cd {{mod}}/tests && dagger functions
  cd {{mod}}/tests && dagger call test-all {{args}}

# Recipe to run all the examples in the target module ğŸ“„
examplesgo mod *args: (reloadmod mod)
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running Dagger module examples (Go SDK)..."
  echo "ğŸ“¦ Currently in {{mod}} module ğŸ§ª, path=`pwd`"
  test -d {{mod}}/examples/go || (echo "âŒ Module examples not found" && exit 1)
  cd {{mod}}/examples/go && dagger call all-recipes {{args}}

# Recipe to run GolangCI Lint ğŸ§¹
golint mod *args:
  #!/usr/bin/env sh
  set -e
  echo "Running Go (GolangCI)... ğŸ§¹ "
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  echo "ğŸ“¦ Currently in {{mod}} module, path=`pwd`/{{mod}}"
  cd ./{{mod}} && nix-shell -p golangci-lint --run "golangci-lint run --config ../.golangci.yml {{args}}"
  echo "ğŸ§ª Checking now the tests project ..."
  cd ./{{mod}}/tests && nix-shell -p golangci-lint --run "golangci-lint run --config ../../.golangci.yml {{args}}"
  echo "ğŸ“„ Checking now the examples project ..."
  cd ./{{mod}}/examples/go && nix-shell -p golangci-lint --run "golangci-lint run --config ../../../.golangci.yml {{args}}"

# Recipe to run the whole CI locally ğŸš€
cilocal mod: (reloadall mod) (golint mod) (test mod) (examplesgo mod) (ci-module-docs mod)
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running the whole CI locally... ğŸš€"

# Recipe to create a new module using Daggy (a rust CLI tool) ğŸ› ï¸
create mod with-ci='false' type='full':
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Creating a new {{type}} module of type {{type}}..."
  cd .daggerx/daggy && cargo build --release
  .daggerx/daggy/target/release/daggy --task=create --module={{mod}} --module-type={{type}}
  if [ "{{with-ci}}" = "true" ]; then just cilocal {{mod}}; fi

# Recipe to create a new light module using Daggy ğŸ› ï¸
createlight mod with-ci='false' type='light':
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Creating a new {{type}} module of type {{type}}..."
  cd .daggerx/daggy && cargo build --release
  .daggerx/daggy/target/release/daggy --task=create --module={{mod}} --module-type={{type}}
  if [ "{{with-ci}}" = "true" ]; then just cilocal {{mod}}; fi

# Recipe to validate if the dagger module has the README.md file and the LICENSE file ğŸ“„
ci-module-docs mod:
  #!/usr/bin/env sh
  set -e
  echo "ğŸ” Validating the module documentation..."
  test -f {{mod}}/README.md || (echo "âŒ README.md file not found" && exit 1)
  test -f {{mod}}/LICENSE || (echo "âŒ LICENSE file not found" && exit 1)
  echo "âœ… Module documentation is valid"

# Recipe for dagger call ğŸ“
call mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running Dagger call..."
  echo "ğŸ“¦ Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  cd {{mod}} && dagger call {{args}}

# Recipe for dagger call tests in a certain module ğŸ§ª
calltests mod *args: (reloadtest mod)
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running Dagger call tests..."
  echo "ğŸ§ª Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}}/tests || (echo "âŒ Module not found" && exit 1)
  cd {{mod}}/tests && dagger functions
  cd {{mod}}/tests && dagger call {{args}}
# Recipe to run dagger develop in all modules ğŸ”„
develop-all:
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Developing (or upgrading) all Dagger modules..."
  cd .daggerx/daggy && cargo build --release
  .daggerx/daggy/target/release/daggy --task=develop

# Recipe that wraps the dagger CLI in a certain module ğŸ“¦
dag mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸš€ Running Dagger CLI in a certain module..."
  echo "ğŸ“¦ Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  cd {{mod}} && dagger {{args}}

# Recipe to call a certain function by a module's name, passing extra arguments optionally ğŸ“
callfn mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸ”§ Calling a function in a certain module..."
  echo "ğŸ“¦ Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  cd {{mod}} && dagger functions
  cd {{mod}} && dagger call {{args}}
# Recipe to list functions in a certain module ğŸ“„
listfns mod *args:
  #!/usr/bin/env sh
  set -e
  echo "ğŸ“„ Listing functions in a certain module..."
  echo "ğŸ“¦ Currently in {{mod}} module, path=`pwd`"
  test -d {{mod}} || (echo "âŒ Module not found" && exit 1)
  cd {{mod}} && dagger functions

# Recipe to run Daggy tests ğŸ§ª
daggy-tests:
  @echo "Running Daggy tests ğŸ§ª ..."
  @cd .daggerx/daggy && cargo build --release
  @cd .daggerx/daggy && cargo test